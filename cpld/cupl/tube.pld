Name     tube            ;
PartNo   00              ;
Date     08/12/2023      ;
Revision 01              ;
Designer David Banks     ;
Company  NA              ;
Assembly None            ;
Location                 ;
Device   f1508isptqfp100 ;

// Using defaults allow fitter command line overrides to work properly
//
//property atmel {cascade_logic  on       };
//property atmel {fast_inlatch   on       };
//property atmel {foldback_logic on       };
//property atmel {logic_doubling on       };
//property atmel {optimize       on       };
//property atmel {output_fast    off      };
//property atmel {pin_keep       off      };
//property atmel {preassign      keep     };
//property atmel {security       off      };
//property atmel {xor_synthesis  off      };

property atmel {preassign      keep     };

$define FIX_PINOUT

$ifdef FIX_PINOUT

pin 25 = h_addr2;
pin 40 = h_addr1;
pin 35 = h_addr0;
pin 30 = h_cs_b;
pin 23 = h_data7;
pin 20 = h_data6;
pin 14 = h_data5;
pin 10 = h_data4;
pin  6 = h_data3;
pin 94 = h_data2;
pin 92 = h_data1;
pin 85 = h_data0;
pin 31 = h_phi2;
pin 29 = h_rdnw;
pin 32 = h_rst_b;
pin 98 = h_irq_b;
pin 97 = drq;
pin 47 = dack_b;
pin 48 = p_addr2;
pin 42 = p_addr1;
pin 41 = p_addr0;
pin 44 = p_cs_b;
pin 52 = p_data7;
pin 56 = p_data6;
pin 61 = p_data5;
pin 65 = p_data4;
pin 69 = p_data3;
pin 75 = p_data2;
pin 80 = p_data1;
pin 83 = p_data0;
pin 45 = p_rd_b;
pin 46 = p_wr_b;
pin 96 = p_rst_b;
pin 99 = p_nmi_b;
pin 79 = p_irq_b;

$ELSE

pin    = h_addr2;
pin    = h_addr1;
pin    = h_addr0;
pin    = h_cs_b;
pin    = h_data7;
pin    = h_data6;
pin    = h_data5;
pin    = h_data4;
pin    = h_data3;
pin    = h_data2;
pin    = h_data1;
pin    = h_data0;
pin    = h_phi2;
pin    = h_rdnw;
pin    = h_rst_b;
pin    = h_irq_b;
pin    = drq;
pin    = dack_b;
pin    = p_addr2;
pin    = p_addr1;
pin    = p_addr0;
pin    = p_cs_b;
pin    = p_data7;
pin    = p_data6;
pin    = p_data5;
pin    = p_data4;
pin    = p_data3;
pin    = p_data2;
pin    = p_data1;
pin    = p_data0;
pin    = p_rd_b;
pin    = p_wr_b;
pin    = p_rst_b;
pin    = p_nmi_b;
pin    = p_irq_b;

$ENDIF

// Force creation of intermediate nodes

node hwrite_r3;
node paccess_r3;

// Intermediate values

hwrite_c   = !h_rdnw & !h_cs_b & !h_addr2 & !h_addr1 & !h_addr0;
hwrite_r1  = !h_rdnw & !h_cs_b & !h_addr2 & !h_addr1 &  h_addr0;
hwrite_r2  = !h_rdnw & !h_cs_b & !h_addr2 &  h_addr1 &  h_addr0;
hwrite_r3  = !h_rdnw & !h_cs_b &  h_addr2 & !h_addr1 &  h_addr0;
hwrite_r4  = !h_rdnw & !h_cs_b &  h_addr2 &  h_addr1 &  h_addr0;

hread_s1   =  h_rdnw & !h_cs_b & !h_addr2 & !h_addr1 & !h_addr0;
hread_r1   =  h_rdnw & !h_cs_b & !h_addr2 & !h_addr1 &  h_addr0;
hread_s2   =  h_rdnw & !h_cs_b & !h_addr2 &  h_addr1 & !h_addr0;
hread_r2   =  h_rdnw & !h_cs_b & !h_addr2 &  h_addr1 &  h_addr0;
hread_s3   =  h_rdnw & !h_cs_b &  h_addr2 & !h_addr1 & !h_addr0;
hread_r3   =  h_rdnw & !h_cs_b &  h_addr2 & !h_addr1 &  h_addr0;
hread_s4   =  h_rdnw & !h_cs_b &  h_addr2 &  h_addr1 & !h_addr0;
hread_r4   =  h_rdnw & !h_cs_b &  h_addr2 &  h_addr1 &  h_addr0;

paccess_r1 = !p_cs_b & !p_addr2 & !p_addr1 &  p_addr0;
paccess_r2 = !p_cs_b & !p_addr2 &  p_addr1 &  p_addr0;
paccess_r3 = !p_cs_b &  p_addr2 & !p_addr1 &  p_addr0;
paccess_r4 = !p_cs_b &  p_addr2 &  p_addr1 &  p_addr0;

// Control Register
// Note: .ce and .clk share a product term, unless .clk is on a global input
node [control6..0];
[control6..0].ck = !h_phi2;
[control6..1].ar = !h_rst_b;
[control0].ap    = !h_rst_b;
control0.t = hwrite_c & !h_data0 & (control0 $ h_data7);
control1.t = hwrite_c & !h_data1 & (control1 $ h_data7);
control2.t = hwrite_c & !h_data2 & (control2 $ h_data7);
control3.t = hwrite_c & !h_data3 & (control3 $ h_data7);
control4.t = hwrite_c & !h_data4 & (control4 $ h_data7);
control5.t = hwrite_c & !h_data5 & (control5 $ h_data7);
control6.t = hwrite_c & !h_data6 & (control6 $ h_data7);

t_flag = control6; // Tube reset: a '1' holds the PH/HP FIFOs in their reset state
p_flag = control5; // Parasite reset a '1->0' transision p_rst_b to be asserted for 80us
v_flag = control4; // Two byte mode: a '1' causes PH3/HP3 to operate in two-byte mode
m_flag = control3; // PNMI from HP3 non-empty / PH3 empty
j_flag = control2; // PIEQ from HP4 non-empty
i_flag = control1; // PIRQ from HP1 non-empty
q_flag = control0; // HIRQ from PH4 non-empty

// HP1/2/4 FIFOs

node [hp124_reg7..0];
node hwrite_r124;
hwrite_r124  = hwrite_r1 # hwrite_r2 # hwrite_r4;
[hp124_reg7..0].ck = !h_phi2;
[hp124_reg7..0].d  = [h_data7..0] & hwrite_r124 # [hp124_reg7..0] & !hwrite_r124;

node hp1_state;
node hp2_state;
node hp4_state;
hp1_state.ck = !h_phi2;
hp1_state.t  = !hp1_state & hwrite_r1;
hp1_state.ar = !h_rst_b # control6 # (!p_rd_b & paccess_r1);
hp2_state.ck = !h_phi2;
hp2_state.t  = !hp2_state & hwrite_r2;
hp2_state.ar = !h_rst_b # control6 # (!p_rd_b & paccess_r2);
hp4_state.ck = !h_phi2;
hp4_state.t  = !hp4_state & hwrite_r4;
hp4_state.ar = !h_rst_b # control6 # (!p_rd_b & paccess_r4);

hp1_dav = hp1_state;
hp2_dav = hp2_state;
hp4_dav = hp4_state;
hp1_sav = !hp1_state & !hp2_state & !hp4_state;
hp2_sav = !hp1_state & !hp2_state & !hp4_state;
hp4_sav = !hp1_state & !hp2_state & !hp4_state;
[hp1_data7..0] = [hp124_reg7..0];
[hp2_data7..0] = [hp124_reg7..0];
[hp4_data7..0] = [hp124_reg7..0];

// PH1/2/4 FIFOS

node [ph124_reg7..0];
node paccess_r124;
paccess_r124 = paccess_r1 # paccess_r2 # paccess_r4;
[ph124_reg7..0].ck = p_wr_b;
[ph124_reg7..0].d  = [p_data7..0] & paccess_r124 # [ph124_reg7..0] & !paccess_r124;

node ph1_state;
node ph2_state;
node ph4_state;
ph1_state.ck = p_wr_b;
ph1_state.t  = !ph1_state & paccess_r1;
ph1_state.ar = !h_rst_b # control6 # (h_phi2 & hread_r1);
ph2_state.ck = p_wr_b;
ph2_state.t  = !ph2_state & paccess_r2;
ph2_state.ar = !h_rst_b # control6 # (h_phi2 & hread_r2);
ph4_state.ck = p_wr_b;
ph4_state.t  = !ph4_state & paccess_r4;
ph4_state.ar = !h_rst_b # control6 # (h_phi2 & hread_r4);

ph1_dav = ph1_state;
ph2_dav = ph2_state;
ph4_dav = ph4_state;
ph1_sav = !ph1_state & !ph2_state & !ph4_state;
ph2_sav = !ph1_state & !ph2_state & !ph4_state;
ph4_sav = !ph1_state & !ph2_state & !ph4_state;
[ph1_data7..0] = [ph124_reg7..0];
[ph2_data7..0] = [ph124_reg7..0];
[ph4_data7..0] = [ph124_reg7..0];

// HP3 FIFO

node [hp3_reg7..0];
[hp3_reg7..0].ck = !h_phi2;
[hp3_reg7..0].d =  [h_data7..0] & hwrite_r3 # [hp3_reg7..0] & !hwrite_r3;

node hp3_state;
hp3_state.ck = !h_phi2;
hp3_state.t  = !hp3_state & hwrite_r3;
hp3_state.ar = !h_rst_b # control6 # (!p_rd_b & paccess_r3);

hp3_dav = hp3_state;
hp3_sav = !hp3_state;
[hp3_data7..0] = [hp3_reg7..0];

// PH3 FIFO

node [ph3_reg7..0];
[ph3_reg7..0].ck = p_wr_b;
[ph3_reg7..0].d =  [p_data7..0] & paccess_r3 # [ph3_reg7..0] & !paccess_r3;

node ph3_state;
ph3_state.ck = p_wr_b;
ph3_state.t  = !ph3_state & paccess_r3;
ph3_state.ar = !h_rst_b # control6 # (h_phi2 & hread_r3);

ph3_sav = !ph3_state;
ph3_dav = ph3_state;
[ph3_data7..0] = [ph3_reg7..0];

// Interrupt locgic

nmi_flag = hp3_dav # ph3_sav;

p_nmi_b = !(m_flag & nmi_flag);

p_irq_b = !((j_flag & hp4_dav) # (i_flag & hp1_dav));

h_irq_b = 'b'0;
h_irq_b.oe = q_flag & ph4_dav;

// Reset logic

p_rst_b = h_rst_b & !p_flag;


// p_data output multiplexor

unused5..0 = 'b'111111;

[p_data7..0] = [nmi_flag, ph1_sav, control5..0] & (!p_addr2 & !p_addr1 & !p_addr0) #
               [hp1_data7..0]                   & (!p_addr2 & !p_addr1 &  p_addr0) #
               [hp2_dav, ph2_sav, unused5..0]   & (!p_addr2 &  p_addr1 & !p_addr0) #
               [hp2_data7..0]                   & (!p_addr2 &  p_addr1 &  p_addr0) #
               [hp3_dav, ph3_sav, unused5..0]   & ( p_addr2 & !p_addr1 & !p_addr0) #
               [hp3_data7..0]                   & ( p_addr2 & !p_addr1 &  p_addr0) #
               [hp4_dav, ph4_sav, unused5..0]   & ( p_addr2 &  p_addr1 & !p_addr0) #
               [hp4_data7..0]                   & ( p_addr2 &  p_addr1 &  p_addr0);

[p_data0..7].oe = !p_rd_b & !p_cs_b;

// h_data output multiplexor

[h_data7..0] = [ph1_dav, hp1_sav, control5..0]  & (!h_addr2 & !h_addr1 & !h_addr0) #
               [ph1_data7..0]                   & (!h_addr2 & !h_addr1 &  h_addr0) #
               [ph2_dav, hp2_sav, unused5..0]   & (!h_addr2 &  h_addr1 & !h_addr0) #
               [ph2_data7..0]                   & (!h_addr2 &  h_addr1 &  h_addr0) #
               [ph3_dav, hp3_sav, unused5..0]   & ( h_addr2 & !h_addr1 & !h_addr0) #
               [ph3_data7..0]                   & ( h_addr2 & !h_addr1 &  h_addr0) #
               [ph4_dav, hp4_sav, unused5..0]   & ( h_addr2 &  h_addr1 & !h_addr0) #
               [ph4_data7..0]                   & ( h_addr2 &  h_addr1 &  h_addr0);

[h_data0..7].oe = h_phi2 & h_rdnw & !h_cs_b;
